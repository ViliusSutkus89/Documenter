plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin' version '2.5.0'
    id 'kotlin-kapt'
    id "com.mikepenz.aboutlibraries.plugin" version "8.9.4"
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.viliussutkus89.documenter"
        minSdk 16
        targetSdk 32

        versionCode 40
        versionName "1.0.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary true
        multiDexEnabled true
        // https://github.com/android/android-test/issues/1433
        // https://issuetracker.google.com/issues/78108767#comment12
        multiDexKeepProguard file('multidex.pro')
    }
    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    splits.abi {
        enable true
        universalApk true
    }

    buildTypes.release {
        minifyEnabled true
        shrinkResources true
        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }

    packagingOptions {
        // These files are identical.
        // Build is not supposed to break when identical files are found in several dependencies
        pickFirst 'lib/armeabi-v7a/libtmpfile.so'
        pickFirst 'lib/arm64-v8a/libtmpfile.so'
        pickFirst 'lib/x86/libtmpfile.so'
        pickFirst 'lib/x86_64/libtmpfile.so'

        // pdf2htmlEX-Android and wvWare-Android are built with the same NDK-23.1 libc++_shared.so
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
    }

    lint.abortOnError = false

    if (System.getenv('SIGNING_KEYFILE') && System.getenv('SIGNING_ALIAS') && System.getenv('SIGNING_PASS')) {
        signingConfigs {
            release {
                storeFile file(System.getenv('SIGNING_KEYFILE'))
                storePassword System.getenv('SIGNING_PASS')
                keyAlias System.getenv('SIGNING_ALIAS')
                keyPassword System.getenv('SIGNING_PASS')
            }
        }
        buildTypes.release.signingConfig signingConfigs.release
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"

    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"

    implementation "androidx.work:work-runtime:$versions.work"
    implementation "androidx.work:work-runtime-ktx:$versions.work"
    implementation "androidx.work:work-multiprocess:$versions.work"
    implementation 'androidx.concurrent:concurrent-futures:1.1.0'

    kapt "androidx.room:room-compiler:$versions.room"
    implementation "androidx.room:room-ktx:$versions.room"

    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'com.viliussutkus89:pdf2htmlex-android:0.18.18'
    implementation 'com.viliussutkus89:wvware-android:1.2.7'

    // AboutLibraries-v10 requires AndroidSDK-21
    implementation "com.mikepenz:aboutlibraries:8.9.4"

    androidTestImplementation "androidx.test.ext:junit:$versions.junit"
    androidTestImplementation "androidx.test.ext:junit-ktx:$versions.junit"

    androidTestImplementation "androidx.test:core-ktx:$versions.test"
    androidTestImplementation "androidx.test:runner:$versions.test"
    androidTestImplementation "androidx.test:rules:$versions.test"

    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.test_espresso"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$versions.test_espresso"
    implementation "androidx.test.espresso:espresso-idling-resource:$versions.test_espresso"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$versions.test_espresso"

    androidTestImplementation 'com.viliussutkus89:assetextractor-android:1.3.2'
}
